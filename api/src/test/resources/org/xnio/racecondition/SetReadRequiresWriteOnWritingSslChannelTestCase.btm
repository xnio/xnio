
RULE write cannot handle NEED_UNWRAP
CLASS org.xnio.ssl.JsseConnectedSslStreamChannel
METHOD handleHandshake
#AT INVOKE org.xnio.ssl.JsseConnectedSslStreamChannel.readRequiresWrite 2
AFTER INVOKE org.xnio.ssl.JsseConnectedSslStreamChannel.handleUnwrapResult
IF TRUE
DO
    debug("write could not handle NEED_UNWRAP... waking red thread before setWriteRequiresRead"),
    signalWake("write cannot handle NEED_UNWRAP", true),
    # if this thread manages to wait without timing out, an assert !readRequiesWrite()  will fail
    waitFor("read thread finished", 300),
    debug("proceeding with assert !readRequiresWrite; setWriteRequiresRead");
ENDRULE

RULE start Read thread
CLASS org.xnio.racecondition.SetReadRequiresWriteOnWritingSslChannelTestCase$Read
METHOD run
AT ENTRY
IF TRUE
DO
    debug("holding read thread"),
    waitFor("write cannot handle NEED_UNWRAP"),
    debug("starting read thread")
ENDRULE

RULE finish Read thread
CLASS org.xnio.racecondition.SetReadRequiresWriteOnWritingSslChannelTestCase$Read
METHOD run
AT EXIT
IF TRUE
DO
    debug("finished read thread... waking write thread"),
    signalWake("read thread finished")
ENDRULE
