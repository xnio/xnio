
RULE enable race condition scenario
CLASS org.xnio.racecondition.SetReadReadyOnHandlingReadableChannelTestCase
METHOD test
AT INVOKE java.lang.Thread.start 1
IF TRUE
DO
    debug("Enabling race condition scenario"),
    flag("race condition enabled for SetReadReadyOnHandlingReadableChannelTestCase")
ENDRULE

RULE handle readable checked read ready is false
CLASS org.xnio.channels.TranslatingSuspendableChannel
METHOD handleReadable
AFTER INVOKE org.xnio.Bits.allAreClear 1
IF flagged("race condition enabled for SetReadReadyOnHandlingReadableChannelTestCase")
DO
    debug("TranslatingSuspendableChannel just checked that read is not ready... waiting for setReadReady"),
    signalWake("read not ready checked", true),
    waitFor("read ready"),
    debug("Now read is ready... proceeding with handleReadable after a check that read is not ready")
ENDRULE

RULE setReadReady after handleReadable checked read ready is false
CLASS org.xnio.channels.TranslatingSuspendableChannel
METHOD setReadReady
AT ENTRY
IF flagged("race condition enabled for SetReadReadyOnHandlingReadableChannelTestCase")
DO
    debug("setReadReady... waiting for handle readable to check read ready is false"),
    waitFor("read not ready checked"),
    debug("Proceeding with setReadReady")
ENDRULE

RULE after setReadReady
CLASS org.xnio.channels.TranslatingSuspendableChannel
METHOD setReadReady
AT EXIT
IF flagged("race condition enabled for SetReadReadyOnHandlingReadableChannelTestCase")
DO
    debug("Finished setReadReady... waking handleReadable"),
    signalWake("read ready", true)
ENDRULE