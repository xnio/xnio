
RULE enable race condition scenario
CLASS org.xnio.racecondition.SetWriteReadyOnHandlingWritableChannelTestCase
METHOD test
AT INVOKE java.lang.Thread.start 1
IF TRUE
DO
    debug("Enabling race condition scenario"),
    flag("race condition enabled for SetWriteReadyOnHandlingWritableChannelTestCase")
ENDRULE

RULE handle writable checked write ready is false
CLASS org.xnio.channels.TranslatingSuspendableChannel
METHOD handleWritable
AFTER INVOKE org.xnio.Bits.allAreClear 1
IF flagged("race condition enabled for SetWriteReadyOnHandlingWritableChannelTestCase")
DO
    debug("TranslatingSuspendableChannel just checked that write is not ready... waiting for setWriteReady"),
    signalWake("write not ready checked", true),
    waitFor("write ready"),
    debug("Now write is ready... proceeding with handleWritable after a check that write is not ready")
ENDRULE

RULE setWriteReady after handleWritable checked write ready is false
CLASS org.xnio.channels.TranslatingSuspendableChannel
METHOD setWriteReady
AT ENTRY
IF flagged("race condition enabled for SetWriteReadyOnHandlingWritableChannelTestCase")
DO
    debug("setWriteReady... waiting for handle writable to check write ready is false"),
    waitFor("write not ready checked"),
    debug("Proceeding with setWriteReady")
ENDRULE

RULE after setWriteReady
CLASS org.xnio.channels.TranslatingSuspendableChannel
METHOD setWriteReady
AT EXIT
IF flagged("race condition enabled for SetWriteReadyOnHandlingWritableChannelTestCase")
DO
    debug("Finished setWriteReady... waking handleWritable"),
    signalWake("write ready", true)
ENDRULE